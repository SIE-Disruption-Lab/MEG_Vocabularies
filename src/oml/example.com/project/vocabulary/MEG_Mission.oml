vocabulary <http://example.com/project/vocabulary/MEG_Mission#> as meg_mission {
	extends <http://uaontologies.com/UA_Foundation/UA_Foundation#> as foundation
    extends <http://uaontologies.com/UA_Core/UA_Information#> as info
//	extends <http://uaontologies.com/UA_Core/UA_Event#> as event
	extends <http://uaontologies.com/UA_Domain/UA_SystemArchitecture#> as sys_arch
	extends <http://uaontologies.com/UA_Domain/UA_Requirements#> as req
	
	// Aspects
	
	aspect OperationalThing
	
	aspect PhysicalThing

    
    // Concepts
    concept Mission < StrategicPhase[
    	restricts some foundation:contains to MissionPhase	
    	restricts some foundation:contains to MissionScenario
    ]
	
	concept MissionPhase < foundation:Process[
		restricts all foundation:isContainedIn to Mission
    	restricts some foundation:isContainedIn to Mission
    ]
    
    concept StrategicPhase < foundation:Process[
    ]
    	
    concept MissionEngineeringThread < foundation:Function, PhysicalThing[
    	restricts some foundation:specificallyDependentOn to foundation:IndependentContinuant
    	restricts all implements to MissionThread
//	    restricts some implements to MissionThread
	    restricts all foundation:contains to MissionFunction
    	restricts some foundation:contains to MissionFunction
//    	restricts all defines to MissionPhase
//    	restricts some defines to MissionPhase
    ]
    
    concept MissionThread < OperationalActivity[
    	restricts all isImplementedBy to MissionEngineeringThread
//    	restricts some isImplementedBy to MissionEngineeringThread
    	restricts all foundation:contains to MissionTask
    	restricts some foundation:contains to MissionTask
    	restricts all req:satisfies to MissionGoal
//    	restricts some req:satisfies to MissionGoal
    	
    ]
    
    concept OperationalActivity < foundation:Process, OperationalThing[
    	
    ]
    
    concept MissionFunction < foundation:Function, PhysicalThing[
    	restricts some foundation:specificallyDependentOn to foundation:IndependentContinuant
    	restricts all implements to MissionTask
//	    restricts some implements to MissionTask
		restricts all foundation:isContainedIn to MissionEngineeringThread
    	restricts some foundation:isContainedIn to MissionEngineeringThread
    ]
    
    concept MissionTask < OperationalActivity[
    	restricts all isImplementedBy to MissionFunction
//    	restricts some isImplementedBy to MissionFunction
		restricts all foundation:isContainedIn to MissionThread
    	restricts some foundation:isContainedIn to MissionThread
    	restricts all req:satisfies to MissionObjective
//    	restricts some req:satisfies to MissionObjective
    ]
    
    
    concept MissionArchitecture < sys_arch:System, OperationalThing[
    	restricts all isImplementedBy to ResourceArchitecture
    	restricts all foundation:contains to OperationalPerformer
    ]
    
    concept ResourceArchitecture < sys_arch:System, PhysicalThing[
    	restricts all implements to MissionArchitecture
    ]
    
    concept CapabilityConfiguration < sys_arch:System, PhysicalThing[
    	restricts all implements to OperationalPerformer
    ]
    
    concept MissionScenario < foundation:Process[
    	restricts all foundation:contains to MissionVignette
//    	restricts some foundation:contains to MissionVignette	
    	restricts all foundation:isContainedIn to Mission
    	restricts some foundation:isContainedIn to Mission
    ]
	
	concept MissionVignette < foundation:Process[
    	restricts all foundation:isContainedIn to MissionScenario
    	restricts some foundation:isContainedIn to MissionScenario	
    ]
    
    concept MissionGoal < req:Requirement[
    	restricts all req:derivesRequirement to MissionObjective
//    	restricts some info:isAbout to Mission
    	restricts some info:prescribes to Mission
    ]
    
    concept MissionObjective < req:Requirement[
    	restricts all req:requirementDerivedFrom to MissionGoal 
//    	restricts some info:isAbout to MissionPhase
    	restricts some info:prescribes to MissionPhase
    ]
    
//    concept ForceComponent < sys_arch:System, OperationalThing[
//    	restricts all foundation:contains to OperationalPerformer
//    	restricts all foundation:isContainedIn to MissionArchitecture
//    	restricts all isImplementedBy to CapabilityConfiguration
//    ]
    
    concept OperationalPerformer < sys_arch:System, OperationalThing[
//    	restricts all foundation:isContainedIn to ForceComponent
		restricts all foundation:isContainedIn to MissionArchitecture
    ]    
    
    concept ScenarioCondition < info:DescriptiveInformationEntity[
    	restricts all info:describes to MissionScenario
    	restricts some info:describes to MissionScenario
    ]
    
    concept VignetteCondition < info:DescriptiveInformationEntity[
    	restricts all info:describes to MissionVignette
    	restricts some info:describes to MissionVignette
    ]
    
    concept Outcome < foundation:Quality[
    	restricts all req:satisfies to MissionGoal
    	restricts all req:satisfies to MissionObjective 
    ]
    
    concept OutcomeDescriptor < info:DescriptiveInformationEntity[
    	restricts all info:describes to Outcome
    	restricts some info:describes to Outcome
    	restricts all informs to Stakeholder
    	restricts some informs to Stakeholder
    ]
    
    concept Stakeholder < foundation:Role
    
    //Relation
	
	relation entity Defines [
		from MissionEngineeringThread
		to MissionPhase
		forward defines
		reverse isDefinedBy
		asymmetric
		irreflexive
	]
	
	relation entity Implements [
		from PhysicalThing
		to OperationalThing
		forward implements
		reverse isImplementedBy
		asymmetric
		irreflexive
	]
	
	relation entity Informs [
		from OutcomeDescriptor
		to Stakeholder
		forward informs
		reverse isInformedBy
		asymmetric
		irreflexive
	]
    
}